group 'com.rox'
version '1.0-SNAPSHOT'
description '6502 Emulator'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'application'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4-rc-1'
    testCompile group: 'org.quicktheories', name: 'quicktheories', version: '0.13'
    testCompile group: 'com.pholser', name: 'junit-quickcheck-core', version: '0.8-alpha-7'
    testCompile group: 'com.pholser', name: 'junit-quickcheck-generators', version: '0.8-alpha-7'
    testCompile group: 'com.nagternal', name : 'spock-genesis', version: '0.6.0'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'


    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11"
        classpath 'net.sf.proguard:proguard-gradle:5.3.3'
    }
}

jacocoTestReport {
    reports {
        xml {
            enabled true
            destination "jacocoTestReport.xml"
        }
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                     exclude: ['**/ui/**'])
        })
    }
}

pitest {
    excludedClasses = ['com.rox.emu.processor.mos6502.dbg.ui.*']
    outputFormats = ['XML']
    timestampedReports = false
}

task runDebugUI(type: JavaExec, group: 'application') {
    println "Running Debugger UI for ${project.name}"
    classpath sourceSets.main.runtimeClasspath
    main = mainClassName = 'com.rox.emu.processor.mos6502.dbg.ui.DebuggerWindow'
}

jar {
    //Include third party dependencies
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes 'Main-Class': 'com.rox.emu.processor.mos6502.dbg.ui.DebuggerWindow'
    }
}

task obfuscatedJar(dependsOn: 'jar', type: proguard.gradle.ProGuardTask) {
    configuration 'src/main/resources/proguard.config'

    injars "build/libs/${rootProject.name}-${version}.jar"
    outjars "build/libs/${rootProject.name}-1.0.jar"
}

